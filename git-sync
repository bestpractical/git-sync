#!/usr/bin/env perl

=head1 NAME

git-sync - synchronize multiple git repositories

=head1 SYNOPSIS

    git-sync [--verbose|--quiet] [--dry-run] [category]

=head1 DESCRIPTION

C<git-sync> is a tool to keep multiple git repositories up-to-date
with their upstream repositories, and to clone new repositories as
they are added to the upstream location.

The updating is done in a manner which preserves your local state, and
I<should> not cause conflicts.  A C<git fetch --all --prune> and C<git
fetch --all --tags> is always done; if the user has no uncommitted
changes, and the merge would be a fast-forward, the current branch is
C<git pull>ed, and the diffstat is printed.  Otherwise, the state of the
repository is shown in a concise manner.

C<git-sync> also understands C<git-svn> clones, and updates them using
the same rules, but using C<git svn fetch> and C<git svn rebase>
instead of C<git fetch> and C<git pull>.

There are four configuration modes:

=over

=item Local

All of the repositories in a local directory (or set of directories)
are updated as above.  If any of these directories overlap with
L</Github> or L</Remote> directories, the repositories are only
updated once.

=item Github

All of a specific user's repositories (or set of users' repositories)
from Github are cloned into a specified directory.  By default, a
writable clone via the SSH is attempted; if this fails, C<git-sync>
falls back to the read-only C<git> protocol.  If a clone already
exists, it is updated as above.

The configuration can provide a login name (and optionally,
authorization token) that will be used to access Github, and (if
possible) also to determine if cloning via SSH (rather than git://) is
possible.

It can also be configured to add, and keep up-to-update, all
repositories in Github's "network" as remotes.

=item Gitolite

Repositories that the user can read from a Gitolite server are cloned.
If a clone already exists, it is updated as above. The set of repositories
can be limited to those matching a path glob or under a subdirectory.

The configuration must provide the hostname of the Gitolite server.
It may optionally specify the username as well (default "git").

=item Remote

All of the repositories in a specific directory (or set of
directories) on a remote (SSH) server are cloned into a specified
directory.  If a clone already exists, it is updated as above.

C<git-sync> attempts to make use of SSH master connections to speed up
clone and update attempts; see C<ControlPath> and C<ControlMaster> in
L<ssh_config>.  A common configuration might be:

    Host githost.example.com
    ControlMaster auto
    ControlPath /home/alexmv/.ssh/master-%r@%h:%p

=back

=head1 CONFIGURATION

Configuration is done through C<git config> files (F<~/.gitconfig> and
the like).  C<sync.I<category>> sections define common synchronization
categories.  The C<into> key is required; it specifies the local path
where repositories will be found or placed.

    [sync "a-local-dir"]
            # All git repositories directly underneath this one will
            # be updated
            into = /home/alexmv/hacking

If a C<github> key is provided, all repositories of that user (or
users) are cloned and updated:

    [sync "a-github-account"]
            # The "github" key makes this category github-based
            github = alexmv
            github = bestpractical
            # All of alexmv's and bestpractical's repositories will be
            # cloned and updated under this directory
            into = /home/alexmv/github

The C<login> and C<token> keys are used to provide github
authentication, though this is not required.  The C<network> key, if
set, causes C<git-sync> to set up (and fetch) the github network as
remotes for each repository, additionally.

If a C<gitoliteHost> key is provided, all repositories available from
that server are cloned and updated:

    [sync "a-gitolite-server"]
            gitoliteHost = example.com
            # If not specified, gitoliteUser defaults to "git"
            gitoliteUser = gitolite

Gitolite sections may include any number of C<path> keys to limit
which repositories are cloned.  Each path that includes a '*' or '?'
is a glob to match repository names.  Otherwise, all repositories
organized in a directory under C<path> are acted on.

The presence of C<host> and C<path> keys configures a remote directory
to clone from.  Multiple values for C<path> can be given; alternately,
if only one path is needed, C<host> can be specified as
C<hostname:/path>.

    [sync "a-remote-dir"]
            # The "host" key makes this category remote-server based
            host = example.com:/git
            # All repositories under /git will be cloned and updated
            # under this directory
            into = /home/alexmv/example.com

If the path includes a '*', it is treated as a list of remote
repository paths.  Otherwise, all subdirectories of the path which are
git repositories are acted on.

Note that local categories can be overlapped with other types; the
following configuration clones and updates all of alexmv's github
repositories, and also updates any other repositories it happens to
find in C</home/alexmv/github>:

    [sync "local"]
            into = /home/alexmv/github
    [sync "github"]
            github = alexmv
            into = /home/alexmv/github

Remote, Github, and Gitolite configurations also support the C<email>
key; if set, newly-cloned repositories will have C<user.email> set to
the given value.

Remote configurations automatically attempt to set up an SSH master
connection.  C<local> configurations can start one or more SSH master
connections by explicitly providing C<sshmaster> configuration lines.

=head1 OPTIONS

=over

=item C<--verbose> or C<-v>

Be more verbose about which repositories are skipped, and why.  This
option's default is controlled by the C<sync.verbose> key in
F<.gitconfig>

=item C<--quiet> or C<--terse> or C<-q>

Skip the display of non-dirty, up-to-date repositories.  This option's
default is controlled bt the C<sync.quiet> key in F<.gitconfig>

=item C<--pretend> or C<--dry-run> or C<-n>

Remote repositories are not fetched from; the list of repositories
that would be synchronized is listed, and their state.  Note that
network access is still needed to obtain a list of repositories for
L</Github>, L</Gitolite>, and L</Remote> directories.

=item C<--log> or C<-l>

When a repository is ahead or behind its tracking remote (but not
diverged), show the output of C<git log --oneline --reverse> for the
list of commits that differ.  This option's default is controlled by
the C<sync.log> key in F<.gitconfig>

=item C<--stash> or C<-s>

Display how many entries there are in the stash, if there are any;
defaults to off.  This option's default is controlled by the
C<sync.stash> key in F<.gitconfig>

=item C<--gc> or <-G>

Run C<git gc> on each repository, repacking it for better disk space
efficiency, and faster operation; as this is a possibly time-intensive
operation, this defaults to off.  This option's default is controlled
by the C<sync.gc> key in F<.gitconfig>

=item C<--help> or C<-h>

Shows this documentation.

=back

Other parameters passed on the command line are taken to be categories
to be synchronized; if no other arguments are given, synchronzies all
categories.

=head1 AUTHOR

Alex Vandiver C<< <alexmv@bestpractical.com> >>

=head1 COPYRIGHT

This program is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut

use 5.008;
use Git;
use strict;
use warnings;
use Term::ANSIColor qw//;
use Getopt::Long;
use Pod::Usage;
use LWP::UserAgent qw();
use File::Path qw();

our $VERSION = 1.0;

sub colored { (-t STDOUT) ? Term::ANSIColor::colored(@_) : $_[0] }

$SIG{INT} = sub {
    print colored("\n\nInterrupted!\n", "bold red");
    exit;
};
$SIG{USR1} = 'IGNORE';

my ($verbose, $quiet, $pretend, $log, $stash, $gc, $help);
GetOptions(
    'verbose|v!' => \$verbose,
    'quiet|terse|q!' => \$quiet,
    'pretend|dry-run|n' => \$pretend,
    'log|l!' => \$log,
    'stash|s!' => \$stash,
    'gc|G!' => \$gc,
    'help|h' => \$help,
) or pod2usage( -verbose => 1 );

pod2usage( -verbose => 0, -msg => "--verbose and --quiet are mutually exclusive")
    if $verbose and $quiet;

pod2usage( -verbose => 2 ) if $help;

$verbose = -$quiet if defined $quiet;

my %sync;
for (split /\0/, `git config -z --get-regexp ^sync\\\\.`) {
    $verbose =  1 if not defined $verbose and $_ eq "sync.verbose";
    $verbose = -1 if not defined $verbose and $_ eq "sync.quiet";
    $log     =  1 if not defined $log     and $_ eq "sync.log";
    $stash   =  1 if not defined $stash   and $_ eq "sync.stash";
    $gc      =  1 if not defined $gc      and $_ eq "sync.gc";
    next unless /^sync\.(.*?)\.([^\n]*)(?:\n(.*))?/;
    $sync{$1}{$2} = exists $sync{$1}{$2}
        ? ref $sync{$1}{$2} ? [@{$sync{$1}{$2}}, $3] : [$sync{$1}{$2}, $3]
        : $3;
}
$verbose ||= 0;

my @categories;
if (@ARGV) {
    for my $name (@ARGV) {
        die qq{Can't find sync named "$name"\n} unless exists $sync{$name};
        push @categories, $name;
    }
} else {
    $_->{local} = 1 for grep {not $_->{host} and not $_->{github} and not $_->{gitolitehost}} values %sync;
    @categories = sort {exists $sync{$a}{local} <=> exists $sync{$b}{local} or $a cmp $b} keys %sync;
    die qq{No sync targets configured!  Edit your ~/.gitconfig to add one.\n}
        unless @categories;
}

my %seen;
my %masters;
for my $name (@categories) {
    print colored("Syncing" . (length $name ? " $name" : "")."\n", "bold");

    if (not $sync{$name}{into}) {
        print colored("  No 'into' set, skipping!\n", "red");
    } elsif (exists $sync{$name}{gitolitehost}) {
        sync_all_gitolite(%{$sync{$name}});
    } elsif (exists $sync{$name}{host}) {
        sync_all_remote(%{$sync{$name}});
    } elsif (exists $sync{$name}{github}) {
        sync_all_github(%{$sync{$name}});
    } else {
        sync_all_local(%{$sync{$name}});
    }
    print " "x45,"\n";
}

stop_masters();

sub sync_all_local {
    my %config = @_;
    my @sshmaster = ref $config{sshmaster} ? @{$config{sshmaster}} : $config{sshmaster};
    unless ($pretend) {
        start_master($_) for grep {defined} @sshmaster;
    }
    foreach my $into ( ref $config{into} ? @{$config{into}} : $config{into} ) {
        unless (-d $into) {
            print colored("  Directory '$into' does not exist!\n", "bold red");
            next;
        }
        for my $root (grep {-d} <$into/*>) {
            $root =~ m{/([^/]+)(?:\.git)?$};
            printf "  %-40s ", $1;
            next if already($root => 0);
            update($root, 0);
        }
    }
}

sub sync_all_github {
    my %config = @_;

    print colored("  Only one value valid for 'into' when 'github' supplied", "red") and return
        if ref $config{into};

    unless (-d $config{into}) {
        print colored("  Creating directory '$config{into}'\n", "bold");
        print colored("  Directory creation failed: $!\n", "bold red") and return
            unless eval { File::Path::mkpath($config{into}) };
    }

    my $decoder = get_json_decoder();
    print colored("  GitHub sync support requires the JSON::Any module\n", "red") and return
        if not $decoder;

    my @accounts = ref $config{github} ? @{$config{github}} : ($config{github});
    for my $acct (@accounts) {
        my $data = github("users/$acct/repos", $config{login}, $config{token}) or next;

        for my $repo (@{$data || []}) {
            my $reponame = $repo->{name};
            my $root = "$config{into}/$reponame";

            printf "  %-40s ", $reponame;
            next if already($root => 1);
            if (-e $root) {
                add_github_remotes(
                    %config,
                    account => $acct,
                    repository => $reponame,
                    fetch => 0,
                ) if $config{network};
                update($root, 1);
            } else {
                my $auth = 1;
                if ($config{login}) {
                    my $data = github("repos/$acct/$reponame/collaborators");
                    $auth = 0 if $data and not grep {$_->{login} eq $config{login}} @{$data};
                }
                if ($auth) {
                    $auth = clone($root => "git\@github.com:$acct/$reponame.git" => $config{email});
                    printf "  %-40s ", "...trying again, anonymously" unless $auth;
                }
                unless ($auth) {
                    clone($root => "git://github.com/$acct/$reponame.git" => $config{email});
                }
                add_github_remotes(
                    %config,
                    account => $acct,
                    repository => $reponame,
                    fetch => 1,
                ) if $config{network};
            }
        }
    }
}

sub add_github_remotes {
    my %config = @_;

    my $data = github("http://github.com/$config{account}/$config{repository}/network_meta") or return;

    my %existing;
    my $repo = Git->repository(Directory => "$config{into}/$config{repository}");
    $existing{$_}++ for map {chomp; $_} $repo->command("remote", "show");

    my @network = sort grep {$_ ne $config{account} and not $existing{$_}} map {$_->{name}} @{$data->{users}};
    return unless @network;
    printf colored("    %-40s ", "dark"), "Adding github network remotes";
    for my $remote (@{$data->{users}}) {
        next if $remote->{name} eq $config{account};
        next if $existing{$remote->{name}};
        $repo->command(
            "remote", "add",
            $remote->{name},
            "git://github.com/" . $remote->{name} . "/" . $remote->{repo} . ".git",
        );
    }
    my $retval = eval { $repo->command( [qw/fetch --all --prune/], STDERR => 0 )
                     && $repo->command( [qw/fetch --all --tags/], STDERR => 0 ); }
        if $config{fetch};

    print colored("{ @network }\n", "dark green");
}

sub sync_all_remote {
    my %config = @_;

    @config{qw/host path/} = ($1, $2) if $config{host} =~ /^(.*?):(.*)/;

    print colored("  No 'path' set, skipping!\n", "red") and return
        unless $config{path};

    print colored("  Only one value valid for 'into' when 'host' supplied!\n", "red") and return
        if ref $config{into};

    unless (-d $config{into}) {
        print colored("  Creating directory '$config{into}'\n", "bold");
        print colored("  Directory creation failed: $!\n", "bold red") and return
            unless eval { File::Path::mkpath($config{into}) };
    }

    start_master($config{host});

    my @paths = ref $config{path} ? @{$config{path}} : ($config{path});
    for my $path (@paths) {
        $path .= '/*' unless $path =~ /\*/;
        my @list = `ssh $config{host} ls -d $path`;
        print colored("  Listing returned ".($? >> 8).", skipping!\n", "bold red") and next if $?;
        for my $repopath (@list) {
            chomp $repopath;
            my ($reponame) = $repopath =~ m{(?:^|/)([^/]*?)(?:\.git)?$};
            my $into = "$config{into}/$reponame";

            printf "  %-40s ", $reponame;
            next if already($into => 1);
            if (-e $into) {
                update($into, 1);
            } else {
                `ssh $config{host} [ -e $repopath/config ]`;
                print $verbose > 0 ? colored("[ Not a git repository ]\n", "dark") : "\r" and next
                    if $?;

                my ($ignore) = `ssh $config{host} git config --bool -f $repopath/config sync.ignore`;
                chomp $ignore if defined $ignore;
                print $verbose > 0 ? colored("[ sync.ignore set ]\n", "dark") : "\r" and next
                    if defined $ignore and $ignore eq 'true';

                clone($into => "$config{host}:$repopath", $config{email});
            }
        }
    }
}

sub sync_all_gitolite {
    my %config = @_;

    my @paths;
    if (ref($config{path})) {
        @paths = @{$config{path}};
    } elsif ($config{path}) {
        @paths = ($config{path});
    } else {
        @paths = ();
    }
    my $path_match;
    if (!@paths) {
        $path_match = ".";
    } else {
        my @path_regexps = map {
            # path can be a glob. If it has no glob characters, we assume it's
            # a "subdirectory" and want all repositories under it. Either way,
            # convert it into a regexp for Gitolite.
            # Gitolite omits leading slashes, so remove it if specified.
            s{^/}{};
            $_ = quotemeta($_);
            if (/\\[*?]/) {
                # Case #1: path is a glob, convert it into a regexp
                s{\\\*}{.*}g;
                s{\\\?}{.}g;
            } else {
                # Case #2: path is a "subdirectory"
                # Ensure the regexp has a trailing slash
                s{/?$}{/};
            }
            $_;
        } @paths;
        $path_match = sprintf("^(%s)", join("|", @path_regexps));
    }

    print colored("  Only one value valid for 'into' when 'gitolitehost' supplied!\n", "red") and return
        if ref $config{into};

    unless (-d $config{into}) {
        print colored("  Creating directory '$config{into}'\n", "bold");
        print colored("  Directory creation failed: $!\n", "bold red") and return
            unless eval { File::Path::mkpath($config{into}) };
    }

    my $gitolite_user = $config{gitoliteuser} || "git";
    my $gitolite_ssh = "$gitolite_user\@$config{gitolitehost}";
    my @gitolite_repos = ();
    my $gitolite_info;
    unless (open($gitolite_info, "-|", "ssh", "$gitolite_ssh", "info", "-p")) {
        print colored("  Could not open gitolite info via SSH!\n", "bold red");
        return;
    }
    while (my $repo_line = <$gitolite_info>) {
        chomp($repo_line);
        my ($repo_perms, $repo_fullpath) = split(/\t/, $repo_line, 2);
        push(@gitolite_repos, $repo_fullpath) if (
            defined($repo_fullpath)
            and $repo_perms =~ /R/
            and $repo_fullpath =~ /$path_match/
            );
    }
    close($gitolite_info);
    if ($?) {
        print colored("  gitolite info exited " . ($? >> 8) . "\n", "bold red");
        return;
    } elsif ($!) {
        print colored("  gitolite I/O error: $!\n", "bold red");
        return;
    } elsif (!@gitolite_repos) {
        print colored("  No repos matched /$path_match/\n", "yellow");
    }

    for my $repopath (@gitolite_repos) {
        my ($reponame) = $repopath =~ m{(?:^|/)([^/]*?)(?:\.git)?$};
        my $into = "$config{into}/$reponame";

        printf "  %-40s ", $reponame;
        next if already($into => 1);
        if (-e $into) {
            update($into, 1);
        } else {
            clone($into => "$gitolite_ssh:$repopath", $config{email});
        }
    }
}

sub start_master {
    my ($host) = @_;
    printf colored("  %-40s ", "dark"), $host;
    my $status = `ssh -O check $host 2>&1`;
    if ( $status =~ /^Master running \(pid=(\d+)\)/) {
        print colored( "[ Already running SSH master (PID $1) ]\n", "dark");
        return;
    } elsif ( $status =~ /^No ControlPath/) {
        print colored( "[ Not configured -- see ControlPath in ssh_config(5) ]\n", "yellow" );
        return;
    }


    local $SIG{USR1} = sub {
        die "Failed to set up SSH master connection\n";
    };

    my $parent = $$;
    my $pid    = fork;
    die "Fork failed: $!" unless $pid >= 0;
    if ( not $pid ) {

        # Start the master
        system("ssh", "-x", "-M", "-N", $host);

        # Signal the parent when we're done; we're still waiting,
        # we'll catch this and abort.
        kill 'USR1', $parent;
        exit;
    }

    sleep 1 while (`ssh -O check $host 2>&1` !~ /Master running \(pid=(\d+)\)/);
    print colored( "[ SSH master started (PID $1) ]\n", "green" );
    $masters{$host} = $pid;
}

sub stop_masters {
    return unless keys %masters;

    print colored("Stopping SSH master connections\n", "dark");

    for my $host (keys %masters) {
        printf colored("  %-40s ", "dark"), $host;
        if (kill 0, $masters{$host}) {
            my $status = `ssh -O exit $host 2>&1`;
            if ($status =~ /Exit request sent/) {
                print colored("[ Done ]\n", "green");
            } else {
                $status =~ s/\r?\n$//; # chomp doesn't remove the \r
                print colored("[ $status ]\n", "red");
            }
        } else {
            print colored("[ Already stopped! ]\n", "red");
        }
    }
    print "\n";
}

sub already {
    my ($path, $force) = @_;
    if ($force and $seen{$path}) {
        print colored( "[ Already synchronized! ]\n", "red");
        return 1;
    } elsif (exists $seen{$path}) {
        print $verbose > 0 ? colored("[ Already synchronized ]\n", "dark") : "\r";
        return 1;
    }
    $seen{$path} = $force;
    return;
}

sub update {
    my ($root, $force) = @_;

    # Already exists, fetch and possibly rebase
    my $repo = eval { Git->repository(Directory => $root) };
    unless ($repo) {
        print $force ? colored("[ Not a git repository! ]\n", "bold red") : "\r";
        return;
    }

    if ( $repo->config_bool( "sync.ignore" )) {
        print $verbose > 0 ? colored("[ sync.ignore set ]\n", "dark") : "\r";
        return;
    }

    my $gitsvn = -e $repo->repo_path . "/svn/.metadata";
    if ($gitsvn) {
        print colored( "[ svn ] ", "dark blue" );
         # git-svn doesn't work with $repo->command, hate
        `git --git-dir @{[$repo->repo_path]} svn fetch -q` unless $pretend;
    } else {
        my $ret = eval { $repo->command( [qw/fetch --all --prune/], STDERR => 0 )
                      && $repo->command( [qw/fetch --all --tags/],  STDERR => 0 ); } unless $pretend;
    }
    kill $? & 127, $$ if $? & 127; # Propagate the signal it died with, if any

    my $error = $@; chomp $error;
    print colored("[ Fetch failed! $error]\n", "bold red") and return if $@;

    print colored("[ Fetch ] ", "dark") if $pretend;

    print colored("[ Bare repository ]\n", "green") and return if $repo->config_bool('core.bare');


    # We want to set up the upstream to be the svn repo temporarily
    if ($gitsvn) {
        my $branch = $repo->command("symbolic-ref", "HEAD"); chomp $branch;
        if ( $branch =~ s{refs/heads/(.*)}{$1} and not $repo->config( "branch.$branch.merge" ) ) {
            $gitsvn = -$gitsvn;
            $repo->command("config", "branch.$branch.remote", ".");
            $repo->command("config", "branch.$branch.merge",  "refs/remotes/git-svn"); # XXX Assumes standard git-svn layout
        }
    }

    my $status = "";
    my ($fh, $ctx) = $repo->command_output_pipe('status');
    $status .= $_ while (<$fh>);
    eval {$repo->command_close_pipe($fh, $ctx);};

    # Take the config back out
    if ($gitsvn and $gitsvn < 0) {
        my $branch = $repo->command("symbolic-ref", "HEAD"); chomp $branch;
        $branch =~ s{refs/heads/(.*)}{$1};
        $repo->command("config", "--remove-section", "branch.$branch");
    }

    # Look for any stash entries
    my @stash = $stash ? $repo->command('stash', 'list') : ();

    # Rebase if there are no changes, it is on a tracking
    # branch, and the result would be a fast-forward
    my ($branch) = $status =~ /^(?:# )?On branch (.*)/m;
    $branch ||= 'Not currently on any branch';
    if ($status =~ /^nothing (?:added )?to commit/m
            and $status =~ /^(?:# )?Your branch is behind '(.*?)' by (\d+) commits?, and can be fast-forwarded/m) {
        my ($tracking, $behind) = ($1, $2);
        # git-pull is a shell script, unfortunately, and
        # thus can't be run via $repo->command( "pull" )
        chdir($root);
        print colored( $pretend ?
                           "[ Would pull $behind commits ($branch) ]" :
                           "[ Pulled $behind commits ($branch) ]",
                       "bold green" );
        print colored( " [ Stash: @{[@stash+0]} ]", "bold yellow") if @stash;
        print "\n";

        print logs($repo, $branch => $tracking);

        my $pull = $pretend ? "" : $gitsvn ? `git merge --stat refs/remotes/git-svn` : `git pull --stat`;
        kill $? & 127, $$ if $? & 127; # Propagate the signal it died with, if any

        # This line will always be present, and is content-free
        $pull =~ s/^First, rewinding head.*?\n//m;
        # Indent, and colorize (git doesn't, because we're not a tty)
        $pull =~ s/^( .*?)(\+*)(-*)$/$1.colored($2,"green").colored($3,"red")/gme;
        $pull =~ s/^/    /gm if length $pull;
        print $pull;

        # GC after the pull, so we get the new objects
        $repo->command("gc", "--quiet") if $gc and not $pretend;
    } else {
        # If we don't pull, GC now, before we possibly bail
        $repo->command("gc", "--quiet") if $gc and not $pretend;
        my $logs = "";
        my $dirty = ($status !~ /^nothing (?:added )?to commit/m) ? 1 : 0;
        if ($status =~ /^(?:# )?Your branch and '.*?' have diverged.*?(\d+) and (\d+) different commit/sm) {
            print colored( "[ Diverged by $1 and $2 commits ($branch) ]", "bold cyan" );
        } elsif ($status =~ /^(?:# )?Your branch is ahead of '(.*?)' by (\d+) commit/m) {
            my ($tracking, $ahead) = ($1, $2);
            print colored( "[ Ahead by $ahead ($branch) ]", "bold blue");
            $logs = logs($repo, $tracking => $branch);
        } elsif ($status =~ /^(?:# )Your branch is behind '(.*?)' by (\d+) commit/m) {
            my ($tracking, $behind) = ($1, $2);
            print colored( "[ Behind by $behind ($branch) ]", "bold magenta");
            $logs = logs($repo, $branch => $tracking);
        } elsif ($verbose < 0 and not $dirty and not @stash) {
            print "\r", " " x 50, "\r";
            return;
        } else {
            print colored( "[ Up-to-date ($branch) ]", "green");
        }
        print colored( " [ Stash: @{[@stash+0]} ]", "bold yellow") if @stash;
        print colored( " [ Dirty ]", "yellow") if $dirty;
        print "\n$logs";
    }

}

sub clone {
    # Path to clone into, path to clone from, user
    my ($into, $from, $email) = @_;
    local $?;

    print colored("[ Would clone $from into $into ]\n", "bold green") and return 1 if $pretend;

    my $ret = eval { Git::command( [clone => "-q" => $from => $into], STDERR => 0 ) };
    if ($@) {
        print colored( "[ Clone failed! ]\n", "bold red");
        print colored( "Error: $@\n", "red") if $verbose > 0;
        return undef;
    }
    kill $? & 127, $$ if $? & 127; # Propagate the signal it died with, if any

    Git::command( config => "--file", "$into/.git/config", "user.email", $email )
          if $email;
    print colored( "[ Cloned! ]\n", "bold green");
    return 1;
}

sub logs {
    my ($repo, $from, $to) = @_;
    return "" unless $log;
    my $logmsg = $repo->command( log => "--oneline", "--reverse", "$from..$to" );
    # Re-colorize and indent
    $logmsg =~ s/^(\S+)/"    ".colored($1,"yellow")/egm;
    return $logmsg;
}

our $JSON_DECODER = undef;
sub get_json_decoder {
    $JSON_DECODER ||= eval { require JSON::Any; JSON::Any->import; JSON::Any->new };
    return $JSON_DECODER;
}

our $UA = undef;
sub github {
    my ($url, $login, $token) = @_;
    $UA ||= LWP::UserAgent->new(
        agent => "git-sync/$VERSION",
        env_proxy => 1,
        keep_alive => 1,
    );

    $url = ($url =~ m|^https?://| ? $url : "https://api.github.com/$url?");
    $url .= "access_token=$token" if $token;

    my $response = $UA->get($url);
    print colored("  GET of $url failed!\n", "bold red")
        . colored("  ".$response->status_line . "\n", "bold red") and return
        unless $response->is_success;

    my $decoder = get_json_decoder();
    my $data = eval {$decoder->jsonToObj($response->content)};
    print colored("  Parsing of GitHub JSON response failed! $@\n", "bold red")
        ."\n".$response->content."\n" and return
        unless defined $data;

    return $data;
}
